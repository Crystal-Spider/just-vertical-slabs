buildscript {
  repositories {
    // These repositories are only for Gradle plugins, put any other repositories in the repository block further below
    maven { url = 'https://maven.minecraftforge.net' }
    mavenCentral()
  }
  dependencies {
    classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
  }
}
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
plugins {
  id 'eclipse'
  id 'maven-publish'
}
apply plugin: 'net.minecraftforge.gradle'

version = "${mcVersion}-${modVersion}-forge"
group = "crystalspider.${modid}"
archivesBaseName = "${modid}"

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
  mappings channel: 'official', version: "${mcVersion}"
  // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg') // Currently, this location cannot be changed from the default.
  runs {
    client {
      workingDirectory project.file('run')
      property 'forge.logging.markers', 'REGISTRIES'
			// https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
      property 'forge.logging.console.level', 'debug'
      property 'forge.enabledGameTestNamespaces', "${modid}"
      mods {
        "${modid}" {
          source sourceSets.main
        }
      }
    }
    server {
      workingDirectory project.file('run')
      property 'forge.logging.markers', 'REGISTRIES'
			// https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
      property 'forge.logging.console.level', 'debug'
      property 'forge.enabledGameTestNamespaces', "${modid}"
      mods {
        "${modid}" {
          source sourceSets.main
        }
      }
    }
    gameTestServer {
      workingDirectory project.file('run')
      property 'forge.logging.markers', 'REGISTRIES'
      // https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
      property 'forge.logging.console.level', 'debug'
      property 'forge.enabledGameTestNamespaces', "${modid}"
      mods {
        "${modid}" {
          source sourceSets.main
        }
      }
    }
    data {
      workingDirectory project.file('run')
      property 'forge.logging.markers', 'REGISTRIES'
      // https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
      property 'forge.logging.console.level', 'debug'
      args '--mod', "${modid}", '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
      mods {
        "${modid}" {
          source sourceSets.main
        }
      }
    }
  }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {}

dependencies {
  minecraft "net.minecraftforge:forge:${mcVersion}-${forgeVersion}"
}

def resourceTarget = 'META-INF/mods.toml'
def intoTargets = ["$rootDir/out/production/resources/", "$rootDir/out/production/${project.name}.main/", "$rootDir/bin/main/"]
def replaceProperties = [
  modTitle: modTitle,
  mcVersion: mcVersion,
  modVersion: modVersion,
  forgeVersion: forgeVersion,
  author: author
]

processResources {
  inputs.properties replaceProperties
  replaceProperties.put 'project', project

  filesMatching(resourceTarget) {
    expand replaceProperties
  }

  intoTargets.each { target ->
    if (file(target).exists()) {
      copy {
        from(sourceSets.main.resources) {
          include resourceTarget
          expand replaceProperties
        }
        into target
      }
      file("${target}/${resourceTarget}").text = file("${target}/${resourceTarget}").text.replaceAll('modid', project.modid)
    }
  }
}

jar {
  manifest {
    attributes([
      "Specification-Title"     : project.modTitle,
      "Specification-Vendor"    : project.author,
      "Specification-Version"   : "1",
      "Implementation-Title"    : project.modTitle,
      "Implementation-Version"  : project.modVersion,
      "Implementation-Vendor"   : project.author,
      "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    ])
  }
}

jar.finalizedBy('reobfJar')

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}
